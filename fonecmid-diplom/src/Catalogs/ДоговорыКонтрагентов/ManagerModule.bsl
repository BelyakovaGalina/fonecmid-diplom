
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// Проверяет наличие договора клиента на дату, при его наличии 
// 	заполняет значения сумм абонентской оплаты и стоимости работы за час
//
// Параметры:
// 	СсылкаНаДокумент - Документы.ДоговорыКонтрагентов
// 	ДатаДокумента - Дата
// 	АбонОплата - Число - абонентская оплата в месяц
// 	ОплатаЗаЧас - Число - стоимость часа работ 
// Возвращаемое значение:
//  Булево
//
Функция ЕстьДоговорСТипомАбонентскаяПлатаНаДату(СсылкаНаДоговор, ДатаДокумента, АбонОплата = 0, ОплатаЗаЧас = 0) Экспорт
	
	Результат = Ложь;

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Ссылка,
		|   ДоговорыКонтрагентов.ВКМ_СуммаАбонентскойПлаты,
		|   ДоговорыКонтрагентов.ВКМ_СтоимостьЧасаРаботы
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора
		|	И НАЧАЛОПЕРИОДА(ДоговорыКонтрагентов.ВКМ_НачалоДействияДоговора, ДЕНЬ) <= &ДатаДокумента
		|	И КОНЕЦПЕРИОДА(ДоговорыКонтрагентов.ВКМ_ОкончаниеДействияДоговора, ДЕНЬ) >= &ДатаДокумента
		|	И ДоговорыКонтрагентов.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбоненскоеОбслуживание);
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаДоговор);
	Запрос.УстановитьПараметр("ДатаДокумента", ДатаДокумента);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Результат = Истина;
		
		АбонОплата = Выборка.ВКМ_СуммаАбонентскойПлаты;
		ОплатаЗаЧас = Выборка.ВКМ_СтоимостьЧасаРаботы;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
